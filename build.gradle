apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'jacoco'

group = 'net.oneandone'
version = '0.1-SNAPSHOT'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// set default encoding
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    // JSR 353: Json API
    compile group: 'org.glassfish', name: 'javax.json', version: '1.0.4'
    compile group: 'io.dropwizard', name: 'dropwizard-core', version: '0.9.2'

    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.4'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'

    testCompile group: 'org.testng', name: 'testng', version: '6.9.6'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '0.9.2'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '2.0.2-beta'
}

test {
    useTestNG()
    jacoco {
        enabled = true
    }
}

jacocoTestReport {
    reports.xml {
        enabled = true
        destination = "${buildDir}/jacoco/coverage.xml"
    }
}

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (hasProperty('signing.keyId') && hasProperty('signing.password') && hasProperty('signing.secretKeyRingFile')) {
    signing {
        sign configurations.archives
    }
}

// --------------------------------------------------------------------------------------------------------------------
// Version of artifact as defined by the task graph
// --------------------------------------------------------------------------------------------------------------------
gradle.taskGraph.whenReady {taskGraph ->
    if ("${version}".matches("[0-9]+\\.[0-9]+\\-SNAPSHOT") && taskGraph.hasTask(':release')) {
        def gitRevision = 'git rev-list HEAD --count'.execute().text.trim()
        version = "${version}".replace('-SNAPSHOT', '') + ".${gitRevision}"

        configurations.testCompile.dependencies.each { dependency ->
            if (dependency.version.contains("SNAPSHOT")) {
                throw new RuntimeException("project contains SNAPSHOT dependency in artifact ${dependency.group}:${dependency.name}:${dependency.version}")
            }
        }
    }
}

// --------------------------------------------------------------------------------------------------------------------
// Release tasks to 'The Central Repository'
// --------------------------------------------------------------------------------------------------------------------

// credentials to 'The Central Repository' (http://central.sonatype.org/pages/ossrh-guide.html)
if (hasProperty('ossrhUsername') && hasProperty('ossrhPassword')) {
    task release(dependsOn: uploadArchives) << {
        println "build release with version ${version}"
    }

    task snapshot(dependsOn: uploadArchives) << {
        println "build snapshot with version ${version}"
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'concierge'
                    packaging 'jar'
                    // optionally artifactId can be defined here
                    description 'API gateway framework'
                    url 'https://github.com/1and1/concierge'

                    scm {
                        connection 'https://github.com/1and1/concierge'
                        developerConnection 'https://github.com/1and1/concierge.git'
                        url 'https://github.com/1and1/concierge'
                    }

                    licenses {
                        license {
                            name 'The Apache License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    organization {
                        name '1&1 Internet Inc.'
                        url 'http://www.1and1.com/'
                    }

                    developers {
                        developer {
                            id 'Germandrummer92'
                            name 'Daniel Draper'
                        }
                        developer {
                            id 'netdevfighter'
                            name 'Tobias Lindenmann'
                        }
                        developer {
                            id 'piranha87'
                            name 'Andreas Runk'
                        }
                        developer {
                            id 'bitionaire'
                            name 'Johann BÃ¶hler'
                        }
                    }
                }
            }
        }
    }
}

